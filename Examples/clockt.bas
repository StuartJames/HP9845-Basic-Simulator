10   PLOTTER IS 13,"GRAPHICS"
20   GCLEAR 
21   OVERLAP
30   PENUP
40   LOCATE 12,112,0,100
50   GRAPHICS
60   SCALE -1,1,-1,1
70   DEG
80   FOR I=0 TO 360 STEP 6
90   PLOT COS(I),SIN(I)
100  IF I/30=INT(I/30) THEN PLOT .8*COS(I),.8*SIN(I)
110  PLOT .9*COS(I),.9*SIN(I)
120  PENUP
130  NEXT I
140  MOVE 1,-1
150  LORG 7
160  LABEL USING "#,6A";"TISSOT"
170  PLOT 0,0
175	 EXIT GRAPHICS												! EXIT GRAPHICS FOR INPUT.
180  INPUT "HOURS ?",H,"MINUTES ?",M
190  IF H>=12 THEN H=H-12
200  T=3600*H+60*M
210  GRAPHICS
241  CALL Ds(.8,(T),T-1)
250  CALL D(.8,T/60,(T-1)/60)
260  CALL D(.5,T/60/12,(T-1)/60/12)
280  WAIT 300
300  T=T+1
310  GOTO 241
315  END
320  SUB D(L,P,P1)
330        DEG
340        PEN -1
341        MOVE 0,0
350        DRAW L*SIN(6*P1),L*COS(6*P1)
351        MOVE 0,0
352        PEN 1
360        DRAW L*SIN(6*P),L*COS(6*P)
390     SUBEND
400  SUB Ds(L,P,P1)
410        DEG
420        PEN -1
430        MOVE .6*SIN(6*P1),.6*COS(6*P1)
440        DRAW L*SIN(6*P1),L*COS(6*P1)
441     !  BEEP
450        PEN 1
460        MOVE .6*SIN(6*P),.6*COS(6*P)
461        DRAW L*SIN(6*P),L*COS(6*P)
470        MOVE 0,0
480        SUBEND
